////////////////////////////////////////
// Talk
////////////////////////////////////////

IF
  NumTimesTalkedTo(0)
  See([PC])
THEN
  RESPONSE #100
    StartDialogueNoSet([PC])
    Continue()
END

////////////////////////////////////////
// Improved Dragon script from tactics
////////////////////////////////////////

IF
  !NumTimesTalkedTo(0)
  !HPPercentLT(Myself,10)
  Detect([GOODCUTOFF.0.0.0.0.SUMMONED])
  Global("FirkraagScoff","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("FirkraagScoff","LOCALS",1)
    DisplayString(Myself,45920)	// Your lackeys will not save you!
    Continue()
END

IF
  !NumTimesTalkedTo(0)
  !HPPercentLT(Myself,10)
  CheckStatLT(Myself,1,STONESKINS)
  GlobalTimerExpired("FirkraagStone","GLOBAL")
THEN
  RESPONSE #100
    ForceSpell(Myself,DRAGON_STONE_SKIN)
    SetGlobalTimer("FirkraagStone","GLOBAL",600) // can recast
    Continue()
END

IF
  !NumTimesTalkedTo(0)
  !HPPercentLT(Myself,10)
  GlobalTimerExpired("REDDRAGONBREATH","GLOBAL")
  Exists(StrongestOf)
THEN
  RESPONSE #100
    SetGlobalTimer("REDDRAGONBREATH","GLOBAL",18)
    ReallyForceSpell(StrongestOf,RED_DRAGON_BREATH)
    ReallyForceSpell(StrongestOf,RED_DRAGON_HIT)
    SetGlobal("FirkraagTaunt","LOCALS",1) 	// gloat!
    Continue()	// continue after innate actions
END

IF
  !NumTimesTalkedTo(0)
  !HPPercentLT(Myself,10)
  Global("FirkraagTaunt","LOCALS",1)
THEN
  RESPONSE #20
    DisplayString(Myself,60511)	// Time to perish, manling!
    SetGlobal("FirkraagTaunt","LOCALS",0)
    Continue()
  RESPONSE #20
    DisplayString(Myself,19063)	// Your life shall be mine!
    SetGlobal("FirkraagTaunt","LOCALS",0)
    Continue()
  RESPONSE #20
    DisplayString(Myself,19054)	// Your life ends here.
    SetGlobal("FirkraagTaunt","LOCALS",0)
    Continue()
  RESPONSE #20
    DisplayString(Myself,60715)	// Your life ends with this!
    SetGlobal("FirkraagTaunt","LOCALS",0)
    Continue()
  RESPONSE #20
    DisplayString(Myself,21728)	// Run down the road on fire
    SetGlobal("FirkraagTaunt","LOCALS",0)
    Continue()
END

IF
  !NumTimesTalkedTo(0)
  !HPPercentLT(Myself,10)
  Range(NearestEnemyOf(Myself),25)
THEN
  RESPONSE #100
    ForceSpell(NearestEnemyOf(Myself),DRAGON_WING_BUFFET)
    Continue()	// continue after innate actions
END

IF
  !NumTimesTalkedTo(0)
  !HPPercentLT(Myself,10)
  Detect(Player1)
  Global("FirkraagRemove","LOCALS",0)
  GlobalTimerExpired("Cast","LOCALS")
THEN
  RESPONSE #100
    ChangeClass(Myself,MAGE)
    SetGlobalTimer("Cast","LOCALS",6)
    SpellNoDec(Player1,WIZARD_DISPEL_MAGIC)
    SetGlobal("FirkraagRemove","LOCALS",1)
END

IF
  !NumTimesTalkedTo(0)
  !HPPercentLT(Myself,10)
  Detect(StrongestOf)
  Global("FirkraagGreater","LOCALS",0)
  GlobalTimerExpired("Cast","LOCALS")
THEN
  RESPONSE #100
    ChangeClass(Myself,MAGE)
    SetGlobalTimer("Cast","LOCALS",6)
    SpellNoDec(StrongestOf,WIZARD_GREATER_MALISON)
    SetGlobal("FirkraagGreater","LOCALS",1)
END

IF
  !NumTimesTalkedTo(0)
  !HPPercentLT(Myself,10)
  Detect(Player1)
  Global("FirkraagLower","LOCALS",0)
THEN
  RESPONSE #100
    ReallyForceSpell(Player1,DRAGON_FEAR)
    ReallyForceSpell(Player1,DRAGON_LOWER_FIRE)
    SetGlobal("FirkraagLower","LOCALS",1)
    Continue()	// continue after innate actions
END

IF
  !NumTimesTalkedTo(0)
  !HPPercentLT(Myself,10)
  Global("FirkraagSilence","LOCALS",0)
  Detect(WeakestOf)
  GlobalTimerExpired("Cast","LOCALS")
THEN
  RESPONSE #100
    ChangeClass(Myself,CLERIC)
    SetGlobalTimer("Cast","LOCALS",6)
    SpellNoDec(WeakestOf,CLERIC_SILENCE_15_FOOT)
    SetGlobal("FirkraagSilence","LOCALS",1)
END

IF
  !NumTimesTalkedTo(0)
  !HPPercentLT(Myself,10)
  Global("FirkraagHaste","LOCALS",0)
  GlobalTimerExpired("Cast","LOCALS")
THEN
  RESPONSE #100
    ChangeClass(Myself,MAGE)
    SetGlobalTimer("Cast","LOCALS",6)
    SpellNoDec(Myself,WIZARD_HASTE)
    SetGlobal("FirkraagHaste","LOCALS",1)
END

IF
  !NumTimesTalkedTo(0)
  !HPPercentLT(Myself,10)
  Detect(NearestEnemyOfType([PC]))
  GlobalTimerExpired("Cast","LOCALS")
THEN
  RESPONSE #100
    ChangeClass(Myself,MAGE)
    SetGlobalTimer("Cast","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),WIZARD_MELF_ACID_ARROW)
END

IF
  !NumTimesTalkedTo(0)
  !HPPercentLT(Myself,10)
  Detect(NearestEnemyOfType([PC]))
  !GlobalTimerExpired("Cast","LOCALS")
THEN
  RESPONSE #100
    Attack(Player1)	// Kill Gorion's Ward!
END

IF
  !NumTimesTalkedTo(0)
  !HPPercentLT(Myself,10)
  !Detect(NearestEnemyOf(Myself))	// (*)
  CombatCounter(0)			// (*)
THEN
  RESPONSE #100
    DisplayString(Myself,26703)		// "Damn, where are they?"
    ForceSpell(Myself,CLERIC_HEAL)
    SetGlobal("FirkraagLower","LOCALS",0)	     // can cast Lower Fire Resistance
    SetGlobal("FirkraagGreater","LOCALS",0)   // can cast Greater Malison
    SetGlobal("FirkraagHaste","LOCALS",0)    // can cast Haste
    SetGlobal("FirkraagSilence","LOCALS",0) // can cast Silence 15'
    SetGlobal("FirkraagRemove","LOCALS",0) // can cast Remove
    Continue()			        // Chasey chasey!
END

IF
  !NumTimesTalkedTo(0)
  !HPPercentLT(Myself,10)
THEN
  RESPONSE #100
    DisplayString(Myself,44109)	// <CHARNAME>? <CHARNAME>?
    AttackOneRound(Player1)	// Chasey chasey
    AttackOneRound(Player1)	// Chasey chasey
END

IF
  !NumTimesTalkedTo(0)
  !HPPercentLT(Myself,10)
  OR(5)
    AttackedBy([ANYTHING],DEFAULT)
    SpellCast([GOODCUTOFF],0)		// triggers on any spell
    SpellCastPriest([GOODCUTOFF],0)	// triggers on any spell
    SpellCastInnate([GOODCUTOFF],0)	// triggers on any ability
    Detect([GOODCUTOFF.0.0.0.0.SUMMONED])
THEN
  RESPONSE #100
    DisplayString(Myself,21726)	// "Are you trying to threaten me?"
    DisplayString(Myself,60511)	// "Time to perish, manling!"
END

IF
  !NumTimesTalkedTo(0)
  !HPPercentLT(Myself,10)
  Global("FirkraagInit","LOCALS",0)
THEN
  RESPONSE #100
    ForceSpell(Myself,DRAGON_STONE_SKIN)
    SetGlobalTimer("FirkraagStone","GLOBAL",600) // can recast
    SetGlobalTimer("REDDRAGONBREATH","GLOBAL",18)
    SetGlobalTimer("Cast","LOCALS",6)
    EquipMostDamagingMelee() // use those claws!
    SetGlobal("FirkraagInit","LOCALS",1)
END

IF
  !NumTimesTalkedTo(0)
  !HPPercentLT(Myself,10)
  Global("FirkraagWarn","LOCALS",0)
THEN
  RESPONSE #100
    DisplayString(Myself,4369)	// "don't try anything stupid"
    SetGlobal("FirkraagWarn","LOCALS",1)
    RandomWalkContinuous()	// Why make it easy for them?
END

////////////////////////////////////////
// Flee
////////////////////////////////////////

IF
  HPPercentLT(Myself,10)
  Global("b#firavatar","GLOBAL",1)
THEN
  RESPONSE #100
    StartCutSceneMode()
    ClearAllActions()
    SetGlobal("b#firavatar","GLOBAL",2)
    ForceSpell([PC],RED_DRAGON_BREATH)
    DisplayStringHead("b#fir02",@51)
    Wait(1)
    ForceSpell(Myself,DRYAD_TELEPORT)
    EndCutSceneMode()
END